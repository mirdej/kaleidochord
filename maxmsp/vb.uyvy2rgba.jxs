<jittershader name="vb.uyvy2rgba">
	<description>
	converts packed uyvy data as rgba data to full chroma rgba data
	</description>
	<param name="q" type="float" default="0." />
	<language name="glsl" version="1.0">
		<bind param="q" program="fp" />
		<program name="vp" type="vertex" source="sh.passthru.xform.vp.glsl" />
		<program name="fp" type="fragment">
<![CDATA[

// texcoords
varying vec2 texcoord0;

// samplers
uniform sampler2DRect tex0;
uniform float q;

mat4 ctr = mat4(
	 1.164,     1.164,     1.164,    0.,
	 0.    ,    -0.388032,  1.99656,  0.,
	 1.582584, -0.805392,  0.,       0.,
	-0.852408,  0.532642, -1.058956, 1.
);

// entry point
void main()
{   
	// round to nearest pixel centered coordinates 
	// (reason for adding 0.5 to account for GL_LINEAR)
	vec2 coord_left = vec2(floor(texcoord0.x)+0.5,texcoord0.y);
	vec2 coord_frak = (texcoord0 - coord_left);
	
	// sample uyvy data points (are actually yvyu since to argb => rgba)
	vec4 yvyu_left = texture2DRect(tex0, coord_left);

	vec3 yuv_val;
	float y0,y1;
	vec4 rgba_val;	

	// uninterpolated luminance from left (y0) and right (y1) 
	// luminance samples within the single macro pixel 
	y0 = yvyu_left.x;
	y1 = yvyu_left.z;

	// pick appropriate luminance sample for our pixel coord	
	yuv_val.x = mix(y0,y1,float(coord_frak.x>=0.));

	// grab chroma sample (no smoothing)
	yuv_val.zy = yvyu_left.yw;

/*
	CCIR 601/ITU-R. more info on http://www.fourcc.org
	Y: 0. - 1. normalized to the range (16-235)
	U/V(Cb/Cr): -0.5 - 0.5 to the range (0-255), 
	though UV looks like it's not using full range 
	here in QT, so we're doing some extra scaling (16-242)

	Y  = 0.299R + 0.587G + 0.114B
	Cb = (B-Y)*0.565
	Cr = (R-Y)*0.713

	with reciprocal versions:
	R = Y + 1.403Cr
	G = Y - 0.344Cb - 0.714Cr
	B = Y + 1.770Cb	

*/		

	// normalize YUV data to appropriate range
	// Y: 0. to 1. normalized to the range (16-235)	
	// U: -0.5 to 0.5 normalized to the range (16-242)
	// V: -0.5 to 0.5 normalized to the range (16-242)
	yuv_val = yuv_val * vec3(1.164,1.128,1.128)-vec3(0.0628,0.5628,0.5628); 

	// YUV to RGB colorspace conversion
	rgba_val.r = dot(yuv_val,vec3(1.,0.,1.403));
	rgba_val.g = dot(yuv_val,vec3(1.,-0.344,-0.714));
	rgba_val.b = dot(yuv_val,vec3(1.,1.770,0.));
	
//	rgba_val.a = 1. - 2.0 * q * yuv_val.x;
	if (q < 0.) { 
		rgba_val.a = clamp(-3. * yuv_val.x + (4. + 3.* q),0.,1.);
	} else  {
		rgba_val.a = clamp(3. * yuv_val.x + (1. - 3.* q),0.,1.);
	}
//	rgba_val.a =  clamp(min(1.0, 1. + 2. * q) - (2. * q *yuv_val.x), 0., 1.);
	
	
	gl_FragColor = rgba_val;
}

]]>
		</program>
	</language>
</jittershader>






